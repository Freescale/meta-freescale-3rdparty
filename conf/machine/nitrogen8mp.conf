#@TYPE: Machine
#@NAME: Boundary Devices Nitrogen8MP
#@SOC: i.MX8 MP
#@DESCRIPTION: Machine configuration for Boundary Devices Nitrogen8MP
#@MAINTAINER: Chris Dimich <chris.dimich@boundarydevices.com>

MACHINEOVERRIDES =. "mx8:mx8m:mx8mp:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc

# Kernel configuration
PREFERRED_PROVIDER_virtual/kernel ??= "linux-boundary"
KERNEL_DEVICETREE = "freescale/imx8mp-nitrogen8mp.dtb \
		     freescale/imx8mp-a0-nitrogen8mp.dtb \
"
KERNEL_IMAGETYPE = "Image"
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

# U-Boot configuration
PREFERRED_PROVIDER_u-boot ??= "u-boot-boundary"
PREFERRED_PROVIDER_virtual/bootloader ??= "u-boot-boundary"
PREFERRED_PROVIDER_imx-atf ??= "imx-atf-boundary"
SPL_BINARY = "spl/u-boot-spl.bin"
UBOOT_DTB_NAME ?= "imx8mp-nitrogen8mp.dtb"
UBOOT_MAKE_TARGET = ""
UBOOT_SUFFIX = "bin"

# Used to define the U-Boot defconfig for different board variants. The naming
# is here as a safeguard mechanism so that a user doesn't build and flash the
# wrong configuration.
#
# This method will be optimized in the future, but for now, use this variable to
# define the defconfig to be built.
BOUNDARY_DEVICES_UBOOT_DEFCONFIG ?= "nitrogen8mp_2gr0"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "${BOUNDARY_DEVICES_UBOOT_DEFCONFIG}_defconfig,sdcard"

IMAGE_BOOTLOADER = "imx-boot"

IMXBOOT_TARGETS = "flash_evk"

DDR_FIRMWARE_NAME = "\
    lpddr4_pmu_train_1d_imem_201904.bin \
    lpddr4_pmu_train_1d_dmem_201904.bin \
    lpddr4_pmu_train_2d_imem_201904.bin \
    lpddr4_pmu_train_2d_dmem_201904.bin \
"

# wic support
WKS_FILE = "sdimage-bootpart.wks"
IMAGE_BOOT_FILES_append = " \
	boot.scr-${MACHINE};boot.scr \
	upgrade.scr-${MACHINE};upgrade.scr \
	imx-boot;u-boot.${BOUNDARY_DEVICES_UBOOT_DEFCONFIG} \
"
WKS_FILE_DEPENDS_mx8 += " \
	u-boot-script-boundary \
	e2fsprogs-native \
	bmap-tools-native \
"

OPTEE_BIN_EXT = "8mp"

SERIAL_CONSOLES = "115200;ttymxc1"

MACHINE_FEATURES += " pci wifi bluetooth"
