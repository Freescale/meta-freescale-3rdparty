#@TYPE: Machine
#@NAME: Boundary Devices Nitrogen8MM
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for Boundary Devices Nitrogen8MM
#@MAINTAINER: Chris Dimich <chris.dimich@boundarydevices.com>

MACHINEOVERRIDES =. "mx8mm:"

IMX_DEFAULT_BSP ?= "nxp"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8a/tune-cortexa53.inc

# Kernel configuration
PREFERRED_PROVIDER_virtual/kernel ??= "linux-boundary"
KERNEL_DEVICETREE = "freescale/imx8mq-nitrogen8m.dtb \
	freescale/imx8mq-nitrogen8m-m4.dtb \
        freescale/imx8mm-nitrogen8mm.dtb \
	freescale/imx8mm-nitrogen8mm_som.dtb \
	freescale/imx8mm-nitrogen8mm_rev2.dtb \
	freescale/imx8mm-nitrogen8mm_rev2-m4.dtb \
	freescale/imx8mm-nitrogen8mm_som-m4.dtb \
	freescale/imx8mm-nitrogen8mm-m4.dtb \
	freescale/imx8mm-nitrogen8mm-tc358743.dtb \
	freescale/imx8mm-nitrogen8mm_som-tc358743.dtb \
"
KERNEL_IMAGETYPE = "Image"
RDEPENDS:${KERNEL_PACKAGE_NAME}-base = ""

# U-Boot configuration
PREFERRED_PROVIDER_u-boot ??= "u-boot-boundary"
PREFERRED_PROVIDER_virtual/bootloader ??= "u-boot-boundary"
PREFERRED_PROVIDER_imx-atf ??= "imx-atf-boundary"
SPL_BINARY = "spl/u-boot-spl.bin"
UBOOT_DTB_NAME ?= "imx8mm-nitrogen8mm_rev2.dtb"
UBOOT_MAKE_TARGET = ""
UBOOT_SUFFIX = "bin"

# Used to define the U-Boot defconfig for different board variants. The naming
# is here as a safeguard mechanism so that a user doesn't build and flash the
# wrong configuration.
#
# This method will be optimized in the future, but for now, use this variable to
# define the defconfig to be built.
BOUNDARY_DEVICES_UBOOT_DEFCONFIG ?= "nitrogen8mm_rev2_2g"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "${BOUNDARY_DEVICES_UBOOT_DEFCONFIG}_defconfig,sdcard"

# Set ATF platform name
ATF_PLATFORM = "imx8mm"

IMXBOOT_TARGETS = "flash_evk"
IMX_BOOT_SOC_TARGET = "iMX8MM"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = "\
    lpddr4_pmu_train_1d_imem.bin \
    lpddr4_pmu_train_1d_dmem.bin \
    lpddr4_pmu_train_2d_imem.bin \
    lpddr4_pmu_train_2d_dmem.bin \
"

IMAGE_BOOT_FILES:append = " \
	boot.scr-${MACHINE};boot.scr \
	upgrade.scr-${MACHINE};upgrade.scr \
	imx-boot;u-boot.${BOUNDARY_DEVICES_UBOOT_DEFCONFIG} \
"

# wic support
WKS_FILE = "sdimage-bootpart.wks"
WKS_FILE_DEPENDS += "u-boot-script-boundary"

SERIAL_CONSOLES = "115200;ttymxc1"

MACHINE_FEATURES += " pci wifi bluetooth"
