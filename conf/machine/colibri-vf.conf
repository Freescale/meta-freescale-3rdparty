#@TYPE: Machine
#@NAME: Toradex Colibri VF50/VF61
#@SOC: VF500/VF610
#@DESCRIPTION: Machine configuration for Toradex Colibri VF50/VF61 powered by Freescale Vybrid SoC
#@MAINTAINER: Stefan Agner <stefan.agner@toradex.com>

MACHINEOVERRIDES =. "vf:vf50:vf60:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa5.inc

PREFERRED_PROVIDER_virtual/kernel_use-nxp-bsp ??= "linux-toradex"

KERNEL_IMAGETYPE = "zImage"

KERNEL_DEVICETREE = " \
    vf500-colibri-eval-v3.dtb \
    vf610-colibri-eval-v3.dtb \
"

KERNEL_DEVICETREE_append_use-nxp-bsp = " \
    vf500-colibri-aster.dtb \
    vf610-colibri-aster.dtb \
"

# U-Boot of our newer release read the Kernel and device tree from static UBI volumes,
# hence no need to deploy the kernel binary in the image itself
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

PREFERRED_PROVIDER_u-boot ?= "u-boot-toradex"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-toradex"
PREFERRED_PROVIDER_u-boot-fw-utils ?= "u-boot-toradex-fw-utils"
PREFERRED_RPROVIDER_u-boot-fw-utils ?= "u-boot-toradex-fw-utils"
PREFERRED_PROVIDER_u-boot-default-script ?= "u-boot-script-toradex"
PREFERRED_PROVIDER_virtual/kernel-module-mcc ?= "kernel-module-mcc-toradex"
PREFERRED_PROVIDER_virtual/kernel-module-mcc-dev ?= "kernel-module-mcc-toradex"
PREFERRED_VERSION_mqxboot ?= "1.%"
PREFERRED_VERSION_weston_use-nxp-bsp = ""

# U-Boot NAND binary includes 0x400 padding required for NAND boot
UBOOT_BINARY = "u-boot-nand.imx"
UBOOT_MAKE_TARGET = "u-boot.imx"
UBOOT_MACHINE ?= "colibri_vf_defconfig"

IMAGE_FSTYPES += "tar.xz ubifs"
# wic support
IMAGE_BOOT_FILES_append = " boot.scr-${MACHINE};boot.scr"
WKS_FILE_DEPENDS_append = " u-boot-default-script"
WKS_FILE = "sdimage-bootpart.wks"

# Enable free --space-fixup (-F) by default, this allows DFU updates
MKUBIFS_ARGS = " -c 8112 -e 124KiB -m 2KiB -F"
UBINIZE_ARGS = " -p 128KiB -m 2048 -s 2048"
UBI_VOLNAME = "rootfs"

SERIAL_CONSOLES = "115200;ttyLP0"

MACHINE_FEATURES += "usbgadget usbhost vfat alsa touchscreen"
