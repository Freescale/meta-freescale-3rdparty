#@TYPE: Machine
#@NAME: Boundary Devices Nitrogen8M Nano
#@SOC: i.MX8 Nano
#@DESCRIPTION: Machine configuration for Boundary Devices Nitrogen8M Nano
#@MAINTAINER: Chris Dimich <chris.dimich@boundarydevices.com>

MACHINEOVERRIDES =. "mx8:mx8m:mx8mn:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc

# Kernel configuration
PREFERRED_PROVIDER_virtual/kernel ??= "linux-boundary"
KERNEL_DEVICETREE = "freescale/imx8mn-nitrogen8mn.dtb \
		     freescale/imx8mn-nitrogen8mn_som.dtb \
		     freescale/imx8mn-nitrogen8_nano.dtb \
"

KERNEL_IMAGETYPE = "Image"
KERNEL_DEFCONFIG = "boundary_defconfig"
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

#Temporarily remove bootloader from building, as there are compilation errors.
WKS_FILE_DEPENDS_mx8_remove = "imx-boot"
# U-Boot configuration
PREFERRED_PROVIDER_u-boot ??= "u-boot-boundary"
PREFERRED_PROVIDER_virtual/bootloader ??= "u-boot-boundary"
PREFERRED_PROVIDER_imx-atf ??= "imx-atf-boundary"
SPL_BINARY = "spl/u-boot-spl.bin"
UBOOT_DTB_NAME ?= "imx8mn-nitrogen8_nano.dtb"
UBOOT_MAKE_TARGET = ""
UBOOT_SUFFIX = "bin"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "nitrogen8_nano_8mn1gr0_defconfig,sdcard"

IMAGE_BOOTLOADER = "imx-boot"

IMXBOOT_TARGETS = "flash_evk"

DDR_FIRMWARE_NAME = "\
    lpddr4_pmu_train_1d_imem.bin \
    lpddr4_pmu_train_1d_dmem.bin \
    lpddr4_pmu_train_2d_imem.bin \
    lpddr4_pmu_train_2d_dmem.bin \
"

# wic support
WKS_FILE = "sdimage-bootpart.wks"
IMAGE_BOOT_FILES_append = " \
	boot.scr-${MACHINE};boot.scr \
"
WKS_FILE_DEPENDS_mx8 += " \
	u-boot-script-boundary \
	e2fsprogs-native \
	bmap-tools-native \
"

SERIAL_CONSOLES = "115200;ttymxc1"

MACHINE_FEATURES += " pci wifi bluetooth"
