#@TYPE: Machine
#@NAME: Boundary Devices Nitrogen6X
#@SOC: i.MX6Q
#@DESCRIPTION: Machine configuration for Boundary Devices Nitrogen6X
#@MAINTAINER: Eric Nelson <eric.nelson@boundarydevices.com>
#
# Note that this machine configuration also supports the SABRE Lite
# reference design and the Nitrogen6X-SOM.
#
# By default, this machine will build for the standard Quad-Core, 1GB
# option.
#
# To build U-Boot for other CPU or memory combinations, you can set
# the UBOOT_MACHINE variable in your local.conf according to the
# following table:
#
# Processor           Memory    Configuration
# --------------      ------    --------------------
# i.MX6Quad/Dual        1GB     nitrogen6q_config
# i.MX6Quad/Dual        2GB     nitrogen6q2g_config
# i.MX6Quad/Dual        4GB     nitrogen6_max_config
# i.MX6Dual-Lite        1GB     nitrogen6dl_config
# i.MX6Dual-Lite        2GB     nitrogen6dl2g_config
# i.MX6Solo           512MB     nitrogen6s_config
# i.MX6Solo             1GB     nitrogen6s1g_config
#
# See this blog post for details:
#    http://boundarydevices.com/u-boot-updates-for-i-mx6-single
#
#

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa9.inc

SOC_FAMILY = "mx6:mx6dl:mx6q"

KERNEL_DEVICETREE = "imx6q-sabrelite.dtb \
                        imx6q-nitrogen6_max.dtb imx6dl-nitrogen6_max.dtb \
                        imx6q-nitrogen6x.dtb imx6dl-nitrogen6x.dtb \
                        imx6dl-nitrogen6_vm.dtb imx6dl-nitrogen6_vm-magstripe.dtb \
"
KERNEL_IMAGETYPE = "uImage"

PREFERRED_PROVIDER_u-boot = "u-boot-boundary"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-boundary"
PREFERRED_VERSION_linux-boundary ?= "3.10.17"

# Use SPI NOR U-Boot by default
IMAGE_BOOTLOADER ?= ""

# Ensure boot scripts will be available at rootfs time
do_rootfs[depends] += "u-boot-script-boundary:do_deploy"

# Boot scripts to install
BOOT_SCRIPTS = "6x_bootscript-${MACHINE}:6x_bootscript"

UBOOT_MACHINE ?= "nitrogen6q_config"

SERIAL_CONSOLE = "115200 ttymxc1"

MACHINE_EXTRA_RRECOMMENDS += "linux-firmware-wl12xx"

MACHINE_FEATURES += " pci wifi bluetooth"
