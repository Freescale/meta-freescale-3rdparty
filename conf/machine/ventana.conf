#@TYPE: Machine
#@NAME: i.MX6Q/DL Ventana Platform
#@SOC: i.MX6Q/DL
#@DESCRIPTION: Machine configuration for Gateworks Ventana boards.
#@MAINTAINER: Pushpal Sidhu <psidhu@gateworks.com>

MACHINEOVERRIDES =. "mx6:mx6dl:mx6q:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa9.inc

PREFERRED_PROVIDER_virtual/bootloader = "u-boot-gateworks-imx"
PREFERRED_PROVIDER_u-boot = "u-boot-gateworks-imx"

PREFERRED_PROVIDER_virtual/kernel ??= "linux-gateworks-imx"
PREFERRED_PROVIDER_virtual/kernel_use-mainline-bsp ??= "linux-fslc"

KERNEL_DEVICETREE  = " \
    imx6q-gw5400-a.dtb \
    imx6q-gw51xx.dtb \
    imx6q-gw52xx.dtb \
    imx6q-gw53xx.dtb \
    imx6q-gw54xx.dtb \
    imx6q-gw551x.dtb \
    imx6q-gw552x.dtb \
    imx6q-gw553x.dtb \
    imx6q-gw560x.dtb \
    imx6dl-gw51xx.dtb \
    imx6dl-gw52xx.dtb \
    imx6dl-gw53xx.dtb \
    imx6dl-gw54xx.dtb \
    imx6dl-gw551x.dtb \
    imx6dl-gw552x.dtb \
    imx6dl-gw553x.dtb \
    imx6dl-gw560x.dtb \
"

KERNEL_IMAGETYPE = "uImage"

# Not yet supported by Linux mainline
KERNEL_DEVICETREE_remove_use-mainline-bsp = "imx6q-gw560x.dtb imx6dl-gw560x.dtb"

MACHINE_FEATURES += "pci wifi bluetooth"

# Add kernel modules
MACHINE_EXTRA_RRECOMMENDS += "\
    kernel-module-sky2 \
"

# Modules autoload
KERNEL_MODULE_AUTOLOAD = "sky2"

# Req'd for boot
MACHINE_ESSENTIAL_EXTRA_RDEPENDS += "\
    kernel-image kernel-devicetree \
    u-boot-script-gateworks-imx \
"

# Bootloader configs:
UBOOT_CONFIG ??= "std"
UBOOT_CONFIG[std] = "gwventana_config"
UBOOT_ENTRYPOINT_use-mainline-bsp = "0x10008000"

SERIAL_CONSOLES = "115200;ttymxc1"

# Override FS Type Creation
IMAGE_FSTYPES = "tar.bz2 multiubi"

# Build the following sizes
MULTIUBI_BUILD ?= "normal large"

UBI_VOLNAME = "rootfs"

# 256MB ('normal' flash layout)
export MKUBIFS_ARGS_normal = "-F -m 2048 -e 124KiB -c 1912 -x zlib"
export UBINIZE_ARGS_normal = "-m 2048 -p 128KiB -s 2048"

# 2GB ('large' flash layout)
export MKUBIFS_ARGS_large = "-F -m 4096 -e 248KiB -c 8124 -x zlib"
export UBINIZE_ARGS_large = "-m 4096 -p 256KiB -s 4096"
